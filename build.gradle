// see https://github.com/jlink/jqwik-spring/blob/main/build.gradle

// gradle enforcer plugin does not support
// spot is not usable since there is a bug when using lambdas
// -> Maven should be kept in order too be able to check the code

plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

static isSnapshotRelease(versionString) {
    versionString.endsWith('SNAPSHOT')
}

ext {
    moduleName = 'net.jqwik.vavr'
    jqwikVavrVersion = '2.1.0-SNAPSHOT'
    jqwikVersion = '1.7.3'
    junitPlatformVersion = '1.9.2'
    vavrVersion = '0.10.4'
    hamcrestVersion = '2.2'
    isSnapshotRelease = isSnapshotRelease(jqwikVavrVersion)
}

repositories {
    mavenCentral()
    maven { url 'https://s01.oss.sonatype.org/content/repositories/snapshots' }
}

group = moduleName
version = jqwikVavrVersion
description = "Jqwik Vavr support module"

dependencies {
    api("net.jqwik:jqwik-api:${jqwikVersion}")

    implementation("io.vavr:vavr:${vavrVersion}")

    testImplementation("org.hamcrest:hamcrest:${hamcrestVersion}")
    testRuntimeOnly("net.jqwik:jqwik-engine:${jqwikVersion}")

    // Only needed to work around intelliJ bug with running all tests in package
    testImplementation("org.junit.platform:junit-platform-launcher:${junitPlatformVersion}")
}

jar {
    archiveBaseName.set('jqwik-vavr')
    archiveVersion.set("${jqwikVavrVersion}")
    manifest {
        attributes('Automatic-Module-Name': "${moduleName}")
    }
}

java {
    withJavadocJar()
    withSourcesJar()
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

compileTestJava {
    options.compilerArgs += '-parameters'
    options.encoding = 'UTF-8'
}

test {
    useJUnitPlatform {
        includeEngines 'jqwik'
    }

    // testLogging.showStandardStreams = true
}

publishing {
    repositories {
        maven {
            // hint: password is in ~/.gradle/gradle.properties
            def ossrhUsername = project.hasProperty('ossrhUsername') ? project.ossrhUsername : ''
            def ossrhPassword = project.hasProperty('ossrhPassword') ? project.ossrhPassword : ''

            credentials {
                username = ossrhUsername
                password = ossrhPassword
            }

            // change URLs to point to your repos, e.g. http://my.org/repo
            def releasesRepoUrl = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
            url = isSnapshotRelease ? snapshotsRepoUrl : releasesRepoUrl
        }
    }
    publications {
        jqwikVavr(MavenPublication) {
            groupId = 'net.jqwik'
            artifactId = 'jqwik-vavr'
            from components.java
            pom {
                groupId = 'net.jqwik'
                name = 'jqwik-vavr'
                description = "Jqwik Vavr support module"
                url = 'https://github.com/Befrish/jqwik-vavr'
                licenses {
                    license {
                        name = 'Eclipse Public License - v 2.0'
                        url = 'http://www.eclipse.org/legal/epl-v20.html'
                    }
                }
                developers {
                    developer {
                        id = 'Befrish'
                        name = 'Benno MÃ¼ller'
                        email = 'info@befrish.de'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/Befrish/jqwik-vavr.git'
                    developerConnection = 'scm:git:git://github.com/Befrish/jqwik-vavr.git'
                    url = 'https://github.com/Befrish/jqwik-vavr'
                }
            }
        }
    }
}

signing {
    if (!isSnapshotRelease) {
        sign publishing.publications.jqwikVavr
    }
}

wrapper {
    gradleVersion = '8.0.2'
    // upgrade with: ./gradlew wrapper
}
